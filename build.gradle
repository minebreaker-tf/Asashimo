buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.15'
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'rip.deadcode'
version '0.2.4-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compile(
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",

            'javax.persistence:javax.persistence-api:2.2',

            'com.google.guava:guava:23.6-jre',
            'com.google.code.findbugs:jsr305:3.0.2',
            'org.slf4j:slf4j-api:1.7.25',
    )
    testCompile(
            'junit:junit:4.12',
            'org.slf4j:jul-to-slf4j:1.7.25', // Guava uses java.util.logging
            'ch.qos.logback:logback-classic:1.2.3',
            'com.google.truth:truth:0.39',
            'org.mockito:mockito-core:2.13.0',

            'com.h2database:h2:1.4.196',
    )
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

// CircleCI somehow fails to execute dokka task, requiring reflection jars. Seems to be the bug of dokka.
if (System.getenv()["CIRCLECI"]) {
    dokka.enabled = false
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    pkg {
        repo = 'test'
        name = 'asashimo'
        licenses = ['MIT']
        vcsUrl = 'https://bitbucket.org/minebreaker_tf/asashimo.git'
    }
    publications = ['bintrayPublication']
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
