buildscript {
    ext.kotlin_version = '1.2.50'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
    }
}

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'rip.deadcode'
version '0.2.8'

repositories {
    mavenCentral()
}

dependencies {
    compile(
            "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",

            'javax.persistence:javax.persistence-api:2.2',

            'com.google.guava:guava:25.1-jre',
//            'com.google.code.findbugs:jsr305:3.0.2',
            'org.slf4j:slf4j-api:1.7.25',
    )
    testCompile(
            'org.junit.jupiter:junit-jupiter-api:5.1.0',
            'org.slf4j:jul-to-slf4j:1.7.25', // Guava uses java.util.logging
            'ch.qos.logback:logback-classic:1.2.3',
            'com.google.truth:truth:0.41',
            'com.google.truth.extensions:truth-java8-extension:0.41',
            'org.mockito:mockito-core:2.18.3',

            'com.h2database:h2:1.4.197',
            'mysql:mysql-connector-java:5.1.46',
            'org.mariadb.jdbc:mariadb-java-client:2.2.5',
            'org.postgresql:postgresql:42.2.2'
    )
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    pkg {
        repo = 'test'
        name = 'asashimo'
        licenses = ['MIT']
        vcsUrl = 'https://bitbucket.org/minebreaker_tf/asashimo.git'
    }
    publications = ['bintrayPublication']
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
